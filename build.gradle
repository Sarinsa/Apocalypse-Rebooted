import groovy.json.JsonSlurper
import groovy.json.JsonOutput

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "org.spongepowered.mixin"

mixin {
    add sourceSets.main, "apocalypse.mixins.refmap.json"
}

version = "${mc_version}-${mod_version}-${mod_state}"
group = 'com.toast.apocalypse'
archivesBaseName = 'Apocalypse-Rebooted'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: "${mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property "mixin.debug.export", "true"
            property 'mixin.env.disableRefMap', 'true'

            arg "-mixin.config=apocalypse.mixins.json"


            mods {
                apocalypse {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.disableRefMap', 'true'
            property "mixin.debug.export", "true"

            arg "-mixin.config=apocalypse.mixins.json"

            mods {
                apocalypse {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'apocalypse', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                apocalypse {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'prog'
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name 'tterrag'
        url 'https://maven.tterrag.com/'
        content {
            includeGroup 'mcjty.theoneprobe'
        }
    }
    flatDir {
        dir 'flat_deps'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    compileOnly fg.deobf("curse.maven:projecte-api-226410:${projecte_api_version}")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")

    //runtimeOnly fg.deobf("blank:Druidcraft-1.16.5:0.4.54")

    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Apocalypse",
            "Specification-Vendor": "Sarinsa",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Sarinsa",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "apocalypse.mixins.json"
        ])
    }
}

task apiJar(type: Jar) {
    classifier = 'api'
    include 'com/toast/apocalypse/api/**/*'
    exclude 'com/toast/apocalypse/api/impl/**/*'

    from sourceSets.main.output
    from sourceSets.main.allSource
}

task updateVersionCheck {
    try {
        File jsonFile = file('version_check.json')

        if (!jsonFile.exists()) {
            println("Version check json not present, strange! Generating new file...")
            jsonFile.createNewFile()
            println("The newly generated file is empty, cause I am lazy. Lets fill it out manually!")
        }
        else {
            def parsedJson = new JsonSlurper().parse(jsonFile)
            Map<String, String> versions = parsedJson.promos

            String modVersion = "${mod_version}"
            String modState = "${mod_state}"
            String mcVersion = "${mc_version}"
            boolean isRecommended = modState == "r"

            String latest = mcVersion + "-latest"
            String recommended = mcVersion + "-recommended"

            // We only operate with one version per branch
            for (String key : versions.keySet()) {
                if (key != recommended && key != latest) {
                    versions.remove(key)
                }
            }
            versions.put(latest, modVersion + "-" + modState)

            if (isRecommended) {
                versions.put(recommended, modVersion + "-" + modState)
            }
            jsonFile.setText(JsonOutput.prettyPrint(JsonOutput.toJson(parsedJson)))
        }
    }
    catch (Exception e) {
        e.printStackTrace();
    }
}

artifacts {
    archives apiJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
